plugins {
    id 'com.android.application'
    id 'androidsvgdrawable'
}

configurations {
    all*.exclude group: 'androidx.viewpager', module: 'viewpager'
    all*.exclude group: 'androidx.core', module: 'core-ktx'
}

dependencies {
    implementation 'androidx.annotation:annotation:1.5.0'
    implementation 'androidx.core:core:1.9.0'
    implementation 'androidx.activity:activity:1.6.0'
    implementation 'androidx.fragment:fragment:1.5.3'
    implementation 'androidx.recyclerview:recyclerview:1.2.1'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.coordinatorlayout:coordinatorlayout:1.2.0'
    implementation 'androidx.viewpager2:viewpager2:1.0.0'
    implementation 'androidx.lifecycle:lifecycle-livedata:2.5.1'
    implementation 'androidx.lifecycle:lifecycle-service:2.5.1'
    implementation 'androidx.sqlite:sqlite:2.2.0'
    implementation "androidx.room:room-runtime:2.4.3"
    annotationProcessor "androidx.room:room-compiler:2.4.3"
    implementation 'org.bitcoinj:bitcoinj-core:0.16.1'
    implementation 'com.google.guava:guava:31.1-android'
    implementation 'com.google.zxing:core:3.5.0'
    //noinspection GradleDependency
    implementation 'com.squareup.okhttp3:okhttp:3.14.9'
    //noinspection GradleDependency
    implementation 'com.squareup.okhttp3:logging-interceptor:3.14.9'
    implementation 'com.squareup.moshi:moshi:1.11.0'
    //noinspection GradleDependency
    implementation 'org.slf4j:slf4j-api:1.7.36'
    implementation 'com.github.tony19:logback-android:2.0.0'
    testImplementation 'junit:junit:4.13.2'
}

ext {
    archivesBaseName = 'bitcoin-wallet'
}

android {
    compileSdkVersion 'android-31'
    buildToolsVersion '33.0.0'

    defaultConfig {
        applicationId 'de.schildbach'
        dexOptions {
            preDexLibraries false
        }
    }

    buildTypes {
        all {
            minifyEnabled true
            shrinkResources false
            proguardFile 'proguard.cfg'
        }
        debug {
            debuggable true
        }
    }

    flavorDimensions 'flavor'
    productFlavors {
        dev {
            dimension 'flavor'
            applicationIdSuffix '.wallet_test'
        }
        prod {
            dimension 'flavor'
            applicationIdSuffix '.wallet'
        }
    }

    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            java.srcDirs = ['src']
            res.srcDirs = ['res']
            assets.srcDirs = ['assets']
        }
        prod {
            res.srcDirs = ['res-prod']
            assets.srcDirs = ['assets-prod']
        }
        test {
            java.srcDirs = ['test']
            resources.srcDirs = ['test']
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    lintOptions {
        abortOnError false
        disable 'MissingTranslation'
    }

    packagingOptions {
        exclude 'META-INF/*.version'
        exclude 'META-INF/*.kotlin_module'
        exclude 'META-INF/proguard/**'
        exclude 'META-INF/services/**'
        exclude 'META-INF/**/coroutines.pro'
        exclude 'DebugProbesKt.bin'
        exclude '**/*.proto'
        exclude 'kotlin/**'
        exclude 'org/bouncycastle/x509/*.properties'
        exclude 'okhttp3/internal/publicsuffix/*'
        exclude 'org/bitcoinj/crypto/mnemonic/wordlist/english.txt'
        exclude 'org/bitcoinj/crypto/cacerts'
        exclude 'org.bitcoin.*.checkpoints.txt'
    }
}

task svgToPngMipmap(type: fr.avianey.androidsvgdrawable.gradle.SvgDrawableTask) {
    from = files('graphics/mipmap')
    to = file('res')
    targetedDensities = ['xxhdpi', 'xxxhdpi']
    outputFormat = 'PNG'
    outputType = 'mipmap'
}

task svgToPngMipmapProd(type: fr.avianey.androidsvgdrawable.gradle.SvgDrawableTask) {
    from = files('graphics-prod/mipmap')
    to = file('res-prod')
    targetedDensities = ['xxhdpi', 'xxxhdpi']
    outputFormat = 'PNG'
    outputType = 'mipmap'
}
